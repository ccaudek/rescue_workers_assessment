TITLE: Model 5b: Bifactor-ESEM (1 G- and 6 S-factors).
DATA:
FILE = "semdata.dat";

VARIABLE:
NAMES =
  scsj1 scoi2 scch3 scis4 scsk5 scoi6 scch7 scsj8 scmi9 scch10 scsj11 scsk12
  scis13 scmi14 scch15 scsj16 scmi17 scis18 scsk19 scoi20 scsj21 scmi22 scsk23
  scoi24 scis25 scsk26
  pos_sc neg_sc ts_sc 
  ptgi_al ptgi_np ptgi_ps ptgi_sp ptgi_ro
  iesr_a iesr_i iesr_h
  cope_pa cope_po
  mspss_fa mspss_fr mspss_so
  neuro_na neuro_sr
  extra_pa extra_so extra_ac
  group
  ;
  
Grouping = group (0=RW 1=CS);

MISSING = .;

USEVARIABLES ARE
  scsj1 scoi2 scch3 scis4 scsk5 scoi6 scch7 scsj8 scmi9 scch10 scsj11 scsk12
  scis13 scmi14 scch15 scsj16 scmi17 scis18 scsk19 scoi20 scsj21 scmi22 scsk23
  scoi24 scis25 scsk26
  ptgi_al ptgi_np ptgi_ps ptgi_sp ptgi_ro
  iesr_a iesr_i iesr_h
  cope_pa cope_po
  mspss_fa mspss_fr mspss_so
  neuro_na neuro_sr
  extra_pa extra_so extra_ac
  ;

! Specifying that we're treating the variables as categorical.
! CATEGORICAL ARE all;

ANALYSIS:
  ESTIMATOR = ML;
  PROCESSORS = 6;
  ROTATION = TARGET (ORTHOGONAL);
  ! ITERATIONS = 10000;
  ! CONVERGENCE = 0.0001;

MODEL:

sk BY 
  scsk5 scsk12 scsk19 scsk23 scsk26 scsj1~0 scsj8~0 scsj11~0 scsj16~0 
  scsj21~0 scch3~0 scch7~0 scch10~0 scch15~0 scis4~0 scis13~0 scis18~0 
  scis25~0 scmi9~0 scmi14~0 scmi17~0 scmi22~0 scoi2~0 scoi6~0 scoi20~0 
  scoi24~0 (*1);

sj BY 
  scsk5~0 scsk12~0 scsk19~0 scsk23~0 scsk26~0 scsj1 scsj8 scsj11 
  scsj16 scsj21 scch3~0 scch7~0 scch10~0 scch15~0 scis4~0 scis13~0 
  scis18~0 scis25~0 scmi9~0 scmi14~0 scmi17~0 scmi22~0 scoi2~0 scoi6~0 
  scoi20~0 scoi24~0 (*1);

ch BY 
  scsk5~0 scsk12~0 scsk19~0 scsk23~0 scsk26~0 scsj1~0 scsj8~0 scsj11~0 
  scsj16~0 scsj21~0 scch3 scch7 scch10 scch15 scis4~0 scis13~0 scis18~0 
  scis25~0 scmi9~0 scmi14~0 scmi17~0 scmi22~0 scoi2~0 scoi6~0 scoi20~0 
  scoi24~0 (*1);

is BY 
  scsk5~0 scsk12~0 scsk19~0 scsk23~0 scsk26~0 scsj1~0 scsj8~0 
  scsj11~0 scsj16~0 scsj21~0 scch3~0 scch7~0 scch10~0 scch15~0
  scis4 scis13 scis18 scis25 scmi9~0 scmi14~0 scmi17~0 scmi22~0
  scoi2~0 scoi6~0 scoi20~0 scoi24~0 (*1);

mi BY 
  scsk5~0 scsk12~0 scsk19~0 scsk23~0 scsk26~0 scsj1~0 scsj8~0 
  scsj11~0 scsj16~0 scsj21~0 scch3~0 scch7~0 scch10~0 scch15~0 scis4~0 
  scis13~0 scis18~0 scis25~0 scmi9 scmi14 scmi17 scmi22 scoi2~0 scoi6~0 
  scoi20~0 scoi24~0 (*1);

oi BY 
  scsk5~0 scsk12~0 scsk19~0 scsk23~0 scsk26~0 scsj1~0 scsj8~0 scsj11~0 
  scsj16~0 scsj21~0 scch3~0 scch7~0 scch10~0 scch15~0 scis4~0 scis13~0 
  scis18~0 scis25~0 scmi9~0 scmi14~0 scmi17~0 scmi22~0 scoi2 scoi6 
  scoi20 scoi24 (*1);

po BY 
  scsk5* scsk12 scsk19 scsk23 scsk26 scch3 scch7 scch10 scch15
  scmi9 scmi14 scmi17 scmi22;

ne BY 
  scsj1* scsj8 scsj11 scsj16 scsj21 scis4 scis13 scis18 scis25
  scoi2 scoi6 scoi20 scoi24;

po@1; 
ne@1;
po WITH sk-oi@0; 
ne WITH sk-oi@0;

PTGI BY
  ptgi_al ptgi_np ptgi_ps ptgi_sp ptgi_ro;

IESR BY
  iesr_a iesr_i iesr_h;
  
COPE BY 
  cope_pa cope_po;

MSPSS BY  
  mspss_fa mspss_fr mspss_so;

NEURO BY 
  neuro_na neuro_sr;

EXTRA BY
  extra_pa extra_so extra_ac;

PO ON COPE MSPSS NEURO EXTRA;
NE ON COPE MSPSS NEURO EXTRA;
SK ON COPE MSPSS NEURO EXTRA;
SJ ON COPE MSPSS NEURO EXTRA;
CH ON COPE MSPSS NEURO EXTRA;
IS ON COPE MSPSS NEURO EXTRA;
MI ON COPE MSPSS NEURO EXTRA;
OI ON COPE MSPSS NEURO EXTRA;
  
PTGI ON PO NE SK SJ CH IS MI OI COPE MSPSS NEURO EXTRA;
IESR ON PO NE SK SJ CH IS MI OI COPE MSPSS NEURO EXTRA;

model indirect:
  PTGI ind COPE;
  PTGI ind MSPSS;
  PTGI ind NEURO;
  PTGI ind EXTRA;
  
  IESR ind COPE;
  IESR ind MSPSS;
  IESR ind NEURO;
  IESR ind EXTRA;


MODEL CS: 

PO ON COPE MSPSS NEURO EXTRA;
NE ON COPE MSPSS NEURO EXTRA;
SK ON COPE MSPSS NEURO EXTRA;
SJ ON COPE MSPSS NEURO EXTRA;
CH ON COPE MSPSS NEURO EXTRA;
IS ON COPE MSPSS NEURO EXTRA;
MI ON COPE MSPSS NEURO EXTRA;
OI ON COPE MSPSS NEURO EXTRA;
  
PTGI ON PO NE SK SJ CH IS MI OI COPE MSPSS NEURO EXTRA;
IESR ON PO NE SK SJ CH IS MI OI COPE MSPSS NEURO EXTRA;

model indirect:
  PTGI ind COPE;
  PTGI ind MSPSS;
  PTGI ind NEURO;
  PTGI ind EXTRA;
  
  IESR ind COPE;
  IESR ind MSPSS;
  IESR ind NEURO;
  IESR ind EXTRA;


! Requesting standardized parameter estimates and modification indices
OUTPUT:
  stdyx;
  ! MODINDICES (ALL);
  
savedata:
  file is nn_m5b_scores.txt;
  save = fscores;
